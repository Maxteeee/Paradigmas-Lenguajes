¿Qué pasa si dos objetos son capaces de responder a un mismo mensaje?

Se produce entonces un concepto fundamental en el paradigma, cuando tenemos 3 objetos
un objeto que envía un mensaje
y dos objetos que implementan el mensaje de dos maneras diferentes (cada uno define un método distinto)


El observador puede cambiar la referencia sin preocuparse por el envío del mensaje, que permanece sin cambios. Decimos entonces que los dos objetos que comparten cierta interfaz en común son polimórficos para ese observador. 


Ojo, no es necesario que los dos objetos polimórficos definan exactamente los mismos métodos. Alcanza con que implementen al menos un mensaje en común

# Ejemplo

Wollok REPL
> ipod.play()
> dvdLiving.play() 	 	
> vhsRecorder.play()

No obstante, está claro que cada artefacto hace cosas diferentes, porque sus implementaciones son diferentes (los métodos no son iguales). Por otra parte, un video reproductor no puede grabar, de la misma manera que no podemos ir a la pista 8 de un cassette. Pero sí es útil que varios objetos compartan un mensaje, para que alguien use a esos objetos dentro de algún contexto.

¿Para quién es bueno eso? No para los objetos que son polimórficos, sino para quienes usamos esos artefactos. 

# Polimorfismo y tipos

object gimenez {
   var dinero = 300000
	
   method dinero() = dinero 
   method pagarA(empleado) { dinero = dinero - empleado.sueldo() }
}

object galvan {
  var sueldo = 15000

  method sueldo() = sueldo
  method sueldo(_sueldo) { sueldo = _sueldo }
}

object baigorria {
  var cantidadEmpanadasVendidas = 100
  var montoPorEmpanada = 15

  method venderEmpanada() {
    cantidadEmpanadasVendidas = cantidadEmpanadasVendidas + 1 
  }

  method sueldo() = cantidadEmpanadasVendidas * montoPorEmpanada
}

¿Quién recibe el mensaje sueldo enviado por gimenez? Y... puede ser baigorria o galvan, viendo sólamente el código no podemos saber. 

¿de qué tipo es el parámetro empleado? en principio, de cualquier objeto que entienda el mensaje sueldo() sin parámetros y que devuelva un número. El sistema de tipos de Wollok sabe que son por el momento dos los objetos posibles: baigorria y galvan.


# Resumen

En este capítulo hemos conocido uno de los conceptos fundamentales del paradigma: el polimorfismo como una herramienta para agrupar objetos que tienen diferente comportamiento (distinto código) pero la misma interfaz. Entonces puedo enviarles mensajes sin saber cómo lo implementan, e incluso sin saber a qué objeto particular le estoy hablando: puedo cambiar la referencia sin tener que hacer cambios.