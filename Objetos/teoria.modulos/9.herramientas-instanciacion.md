# Instanciacion SIN parametros

```wollok
class Ave {
 	var energia = 100
 
 	method volar(kms) { energia = energia - (kms * 5) }
}
```

sabemos que para crear un ave en la consola podemos hacerlo de esta manera:

>>> const cormoran = new Ave()

¿Cómo queda inicializado el cormorán? En este caso hay una sola referencia, energía, que tiene como valor 100. Podemos enviar el mensaje volar sin problemas, el objeto está correctamente construido.

# Instanciacion CON parametros

Wollok permite inicializar las referencias al construir el objeto, lo que ayuda a darle más claridad y precisión a la instanciación y garantizar que quede bien construido. 

```wollok
class Presentacion {
 	var property lugar
 	const property musicos
 	var property valorEntrada
 	var property entradasVendidas
}
```

La ocasión para darle un valor coherente a cada variable es en el momento de la instanciación, y lo hacemos de esta manera:


```wollok
const seruEnLuna = new Presentacion(
lugar = lunaPark, 
musicos = [seruGiran], 
valorEntrada = 2000, 
entradasVendidas = 180)
```
Ademas se pueden hacer en cualquier orden

```wollok
const seruEnLuna = new Presentacion(
entradasVendidas = 180,
lugar = lunaPark, 
musicos = [seruGiran], 
valorEntrada = 2000)
```

### Var/Const con valor inicial definido

Para aquellas referencias, ya sean variables o constantes, que tengan definido un valor inicial, es opcional enviar un valor por parámetro. 
- En caso que no se le envíe, mantiene en valor con que se inicializó.
- En caso que se envíe un valor por parámetro, se asigna éste en vez del definido en la inicialización.

La instanciación del objeto es un proceso de una sola pasada, que incluye la inicializacion de variables y su asignación mediante los valores que le pasamos a los parámetros. 

De esta manera, en el caso de una referencia constante que tiene un valor inicial y a la vez recibe un valor por parámetro, no se considera este seteo una modificación de la referencia, sino que ambas asignaciones se asumen como la asignación inicial. 

