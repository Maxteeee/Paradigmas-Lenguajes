Vamos a seguir los temas de encapsulamiento delegacion y polimorfismo con el mismo ejemplo para tener un hilo conductor a los temas

# Ejemplo de clase
Tenemos 2 pajaros: Josefa, beti y un entrenador.

```wollok

 object josefa {
 	var gramosComidos = 0
 	var kilometrosVolados = 0
 	const energiaInicial = 80
 	
 	method energia(){
 		return energiaInicial + 5 * gramosComidos - 3 * kilometrosVolados
 	}

 	method comer(gramos){
 		gramosComidos = gramosComidos + gramos
 	}
 	method volar(kilometros){
 		kilometrosVolados = kilometrosVolados + kilometros
 	}
 	
 	method estadoEmocional(){
 		if(gramosComidos > kilometrosVolados)
 		  return "Bonita y gordita"
 		if(energiaInicial < self.energia())
 		  return "Energica"
 		if(self.volo() && not self.comio())
 		  return "Explotada"
 		return "Indiferente"
 	}
 	
 	method comio(){
 		return gramosComidos > 0
 	}
 	
 	method volo(){
 		return kilometrosVolados > 0
 	}
 }
```

```wollok

object beti {
	var companiera = josefa 
	method companiera(nuevaCompaniera){
		companiera = nuevaCompaniera
	}
	method comer(gramos){
		companiera.comer(gramos/2)
	}
	method volar(kms){
		companiera.volar(kms)
	}
	method energia(){
		return companiera.energia()
	}
}

```

```wollok

object entrenador {
 	method entrenar(pajaro){
 		pajaro.comer(10)
 		pajaro.volar(20)
 		if(pajaro.energia() < 20){
 			pajaro.comer(10)
 		} else {
 			pajaro.comer(2)
 		}
 	}
 }

```

# Polimorfismo

## Que es?

**Polimorfismo** significa que **distintos objetos pueden responder al mismo mensaje**, pero con **comportamientos diferentes**.

En Wollok, no hace falta que los objetos compartan una clase o herencia. Basta con que **implementen los mismos métodos**.

Veamos el entrenador

```wollok
object entrenador {
  method entrenar(pajaro){
    pajaro.comer(10)
    pajaro.volar(20)
    if(pajaro.energia() < 20){
      pajaro.comer(10)
    } else {
      pajaro.comer(2)
    }
  }
}
```

Este metodo entrenar, no sabe que tipo de objeto es pajaro, le manda mensajes y confia en que objeto sabra responder a esos mensajes.

## Polimorfismo en accion

Siguiendo nuestro ejemplo, podemos entrenar a distintos objetos con el mimso metodo

```wollok
entrenador.entrenar(josefa)
entrenador.entrenar(beti)
```

En ambos casos, beti y josefa, el mensaje es el mismo (comer, volar, etc.), pero la forma de responderlo varía según el objeto.

Eso es polimorfismo.

## Utilidad

Sirve porque permite escribir código genérico, sin conocer los detalles de cada objeto, y facilita la extensibilidad: podés agregar nuevos tipos de objetos que funcionen sin cambiar el código existente.

## Polimorfismo en WOLLOK

Solo necesitás que los objetos implementen los mismos métodos.

No hace falta definir interfaces o herencias (aunque Wollok lo permite si usás clases).