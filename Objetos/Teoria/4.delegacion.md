Vamos a seguir los temas de encapsulamiento delegacion y polimorfismo con el mismo ejemplo para tener un hilo conductor a los temas

# Ejemplo de clase
Tenemos 2 pajaros: Josefa, beti y un entrenador.

```wollok

 object josefa {
 	var gramosComidos = 0
 	var kilometrosVolados = 0
 	const energiaInicial = 80
 	
 	method energia(){
 		return energiaInicial + 5 * gramosComidos - 3 * kilometrosVolados
 	}

 	method comer(gramos){
 		gramosComidos = gramosComidos + gramos
 	}
 	method volar(kilometros){
 		kilometrosVolados = kilometrosVolados + kilometros
 	}
 	
 	method estadoEmocional(){
 		if(gramosComidos > kilometrosVolados)
 		  return "Bonita y gordita"
 		if(energiaInicial < self.energia())
 		  return "Energica"
 		if(self.volo() && not self.comio())
 		  return "Explotada"
 		return "Indiferente"
 	}
 	
 	method comio(){
 		return gramosComidos > 0
 	}
 	
 	method volo(){
 		return kilometrosVolados > 0
 	}
 }
```

```wollok

object beti {
	var companiera = josefa 
	method companiera(nuevaCompaniera){
		companiera = nuevaCompaniera
	}
	method comer(gramos){
		companiera.comer(gramos/2)
	}
	method volar(kms){
		companiera.volar(kms)
	}
	method energia(){
		return companiera.energia()
	}
}

```

```wollok

object entrenador {
 	method entrenar(pajaro){
 		pajaro.comer(10)
 		pajaro.volar(20)
 		if(pajaro.energia() < 20){
 			pajaro.comer(10)
 		} else {
 			pajaro.comer(2)
 		}
 	}
 }

```

# DELEGACION

## Que es?

La **delegación** ocurre cuando un objeto **le pasa la responsabilidad de hacer algo a otro objeto**.  

En lugar de hacer la tarea por sí mismo, el objeto le **envía el mensaje a otro objeto que él conoce**. 

Si vemos a beti, ella no hace nada por sí misma: todos los mensajes los delega en companiera. Esto incluye comer, volar, calcular energía... ¡todo lo hace la compañera!

## Delegación es distinto de  Abstracción

- **Delegación:** un objeto **le pasa a otro la responsabilidad de hacer algo**.  
  Ejemplo: `beti.comer(10)` → lo realiza su `companiera`, no `beti`.

- **Abstracción:** un objeto **oculta los detalles internos de cómo realiza una acción**, pero **él mismo sigue ejecutándola**.  
  Ejemplo: `josefa.energia()` devuelve un cálculo complejo, pero quien lo llama **no necesita conocer la fórmula interna**.

En resumen:  
> Delegar = “no lo hago yo, lo hace otro”  
> Abstraer = “lo hago yo, pero oculto cómo”

## Delegacion es distnito de herencia
Todavia no esta explicado si seguis el orden de los .md pero

- En la herencia, una clase hereda comportamiento de otra.
- En la delegación, un objeto no hereda nada, simplemente le pide a otro que haga algo por él.