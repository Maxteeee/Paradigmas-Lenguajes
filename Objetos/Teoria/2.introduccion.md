En Wollok, **todo es un objeto**: números, cadenas, booleanos, funciones... y por supuesto, los objetos que vos definís.

# Ejemplo de clase
Tenemos 2 pajaros: Josefa, beti y un entrenador.

```wollok

 object josefa {
 	var gramosComidos = 0
 	var kilometrosVolados = 0
 	const energiaInicial = 80
 	
 	method energia(){
 		return energiaInicial + 5 * gramosComidos - 3 * kilometrosVolados
 	}

 	method comer(gramos){
 		gramosComidos = gramosComidos + gramos
 	}
 	method volar(kilometros){
 		kilometrosVolados = kilometrosVolados + kilometros
 	}
 	
 	method estadoEmocional(){
 		if(gramosComidos > kilometrosVolados)
 		  return "Bonita y gordita"
 		if(energiaInicial < self.energia())
 		  return "Energica"
 		if(self.volo() && not self.comio())
 		  return "Explotada"
 		return "Indiferente"
 	}
 	
 	method comio(){
 		return gramosComidos > 0
 	}
 	
 	method volo(){
 		return kilometrosVolados > 0
 	}
 }
```

```wollok

object beti {
	var companiera = josefa 
	method companiera(nuevaCompaniera){
		companiera = nuevaCompaniera
	}
	method comer(gramos){
		companiera.comer(gramos/2)
	}
	method volar(kms){
		companiera.volar(kms)
	}
	method energia(){
		return companiera.energia()
	}
}

```

```wollok

object entrenador {
 	method entrenar(pajaro){
 		pajaro.comer(10)
 		pajaro.volar(20)
 		if(pajaro.energia() < 20){
 			pajaro.comer(10)
 		} else {
 			pajaro.comer(2)
 		}
 	}
 }

```

## Objeto
 
Un **objeto** es una unidad que combina:

- **Estado** (atributos o variables)
- **Comportamiento** (métodos): Un método es una función que forma parte de un objeto. Define el comportamiento del objeto.

## Atributos

Los atributos son variables propias de un objeto que definen su estado interno. Solo ese objeto puede accederlos directamente. En JOSEFA tenemos

```wollok
var gramosComidos = 0
var kilometrosVolados = 0
const energiaInicial = 80
```

Estos valores cambian a lo largo del tiempo dependiendo de lo que haga el objeto.

## Metodo

Un método es una acción que un objeto puede realizar. Hay 2 tipos de metodos por asi decirlos los de EFECTO que modifican un atributo y los de consulta.

```wollok
method comer(gramos){
  gramosComidos = gramosComidos + gramos
}
```

Ese metodo MODIFICA el atributo gramosComidos (efecto). 

Si queremos consultar el valor de un atributo, planteamos un metodo de consulta que no reciba nada y me retorne el valor.

```wollok
 	method energia(){
 		return energiaInicial + 5 * gramosComidos - 3 * kilometrosVolados
 	}
```

## Mensaje

Cuando llamamos a un metodo de un objeto desde afuera, decimos que le enviamos un mensaje. Desde afuera puede ser la consola u otro objeto

```wollok
josefa.volar(20)
```

o dentro del objeto entrenador

```wollok
method entrenar(pajaro){
 		pajaro.comer(10)
}
```

Siendo pajaro cualquier pajaro que le pasemos por parametro y tenga definido el metodo comer.

## Identidad

Aunque varios objetos se comporten igual, cada objeto tiene su propia identidad.

Por ejemplo, en nuestro programa todos los pajaros pueden comer volar y ver su energia, pero aun asi siguen siendo objetos distitnos.

## Referencia

Una referencia es como un "puntero" a un objeto. En Wollok, cuando guardás un objeto dentro de otro, no lo estás copiando: simplemente estás guardando una referencia a ese objeto.

En el pajaro BETI tenemos

var companiera = josefa 

entonces cuando beti manda mensajes a companiera como

companiera.comer(gramos/2)

en realidad se los anda a josefa y esta misma modifica sus atributos.

## SELF

self se usa para referirse al propio objeto desde dentro de uno de sus métodos. Es útil cuando querés dejar en claro que estás llamando a tus propios métodos o atributos.

En JOSEFA

if(self.volo() && not self.comio())

Nota menor: Podemos negar con el NOT como con el ! igual que en C. Es indistitno aunque mejor usar el NOT

