Vamos a seguir los temas de encapsulamiento delegacion y polimorfismo con el mismo ejemplo para tener un hilo conductor a los temas

# Ejemplo de clase
Tenemos 2 pajaros: Josefa, beti y un entrenador.

```wollok

 object josefa {
 	var gramosComidos = 0
 	var kilometrosVolados = 0
 	const energiaInicial = 80
 	
 	method energia(){
 		return energiaInicial + 5 * gramosComidos - 3 * kilometrosVolados
 	}

 	method comer(gramos){
 		gramosComidos = gramosComidos + gramos
 	}
 	method volar(kilometros){
 		kilometrosVolados = kilometrosVolados + kilometros
 	}
 	
 	method estadoEmocional(){
 		if(gramosComidos > kilometrosVolados)
 		  return "Bonita y gordita"
 		if(energiaInicial < self.energia())
 		  return "Energica"
 		if(self.volo() && not self.comio())
 		  return "Explotada"
 		return "Indiferente"
 	}
 	
 	method comio(){
 		return gramosComidos > 0
 	}
 	
 	method volo(){
 		return kilometrosVolados > 0
 	}
 }
```

```wollok

object beti {
	var companiera = josefa 
	method companiera(nuevaCompaniera){
		companiera = nuevaCompaniera
	}
	method comer(gramos){
		companiera.comer(gramos/2)
	}
	method volar(kms){
		companiera.volar(kms)
	}
	method energia(){
		return companiera.energia()
	}
}

```

```wollok

object entrenador {
 	method entrenar(pajaro){
 		pajaro.comer(10)
 		pajaro.volar(20)
 		if(pajaro.energia() < 20){
 			pajaro.comer(10)
 		} else {
 			pajaro.comer(2)
 		}
 	}
 }

```

# Encapsulamiento

El **encapsulamiento** es uno de los principios fundamentales de la programación orientada a objetos. Consiste en **ocultar los detalles internos de un objeto** y exponer solo lo necesario a través de **métodos**. Así se protege la integridad de los datos del objeto y se mejora la modularidad del código.

##  Que se encapsula?

Se encapsulan los **atributos** del objeto. Es decir, las variables internas **no deberían ser accedidas ni modificadas directamente desde fuera del objeto**.

En Wollok, eso se logra usando `var` y `const`, y accediendo a los datos **solo a través de métodos**.

Ejemplo si vemos JOSEFA
- Los atributos no se acceden desde afuera (josefa.gramosComidos estaría mal).
- Se modifican solo mediante métodos (comer, volar).
- Se accede al valor calculado de energía mediante el método energia().

## Metodos de consulta

Otro ejemplo de encapsulamiento son los métodos que permiten consultar información sin acceder directamente a los atributos:

```wollok
method comio() {
  return gramosComidos > 0
}

method volo() {
  return kilometrosVolados > 0
}
```

Con estos métodos, otros objetos pueden preguntar “¿comiste?” o “¿volaste?” sin necesidad de saber cómo se representa eso internamente.

## Evita romper el encapsulamiento

Si desde otro objeto haces

```wollok
josefa.gramosComidos = 999
```

Estarías rompiendo el encapsulamiento, porque modificás directamente los datos internos de josefa. Esto no se debe hacer. Siempre usá los métodos definidos por el objeto.

## Encapsulamiento debil

```wollok
object beti {
  var companiera = pepita

  method companiera(nuevaCompaniera){
    companiera = nuevaCompaniera
  }
}
```

El atributo companiera puede ser cambiado mediante el método companiera(...). Eso está bien, si se justifica la necesidad de cambiarlo. Pero si se hace sin control, puede ser un punto débil del encapsulamiento.

El encapsulamiento no significa que no se pueda cambiar un valor, sino que el cambio debe estar controlado por el objeto.